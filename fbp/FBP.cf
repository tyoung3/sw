-- FBP.cf 

--  See https://github.com/flowbased/fbp#readme

entrypoints		ValidFBP ;

Valid.	 	    	ValidFBP ::= [Stm];

Stma.			Stm      ::=  String "->"  Prt Prcss Comp; 
Stmf.            	Stm      ::=  Flow;
Stmi.			Stm	 ::= "INPORT"  "=" Ident "." Ident ":" Ident; 
Stmo.			Stm	 ::= "OUTPORT" "=" Ident "." Ident ":" Ident; 
-- NOTE:  Stm ::= "#" "@" Ident String;  NOTE: Conflicts with comment stm. 
--     So need preprocessor (or Lexer) to drop the "#" allowing Stmx:
Stmx.			Stm	 ::= "@" Ident Meta;   
separator	    	Stm      "," ;

Flowa.			Flow     ::=  Srce     "->" Snk; 
FLowb.			Flow     ::=  Flow Prt "->" Snk;

Srca.			Srce     ::=  Prcss  Comp Prt ;
Srcb.			Srce     ::=  Prcss  Prt ;

Snka.			Snk	 ::=  Prt Prcss Comp;
Snkb.			Snk      ::=  Prt Prcss;

Prcssa.			Prcss    ::=  Ident;

Prta.			Prt	 ::=  Ident;

Compa.			Comp	 ::=  "(" Compn ")";

Compma.			Compn	 ::=  Ident;
Compmb.			Compn	 ::=  Ident ":" Meta;
Compme.			Compn	 ::=  ; 

Metai.			Meta	 ::=  Ident;
Metas.			Meta	 ::=  String;
Metae.			Meta	 ::=  Meta "=" Meta;
Metad.			Meta	 ::=  Meta "-" Meta;
Metac.			Meta	 ::=  Meta "," Meta;

comment "#";
-- ?? comment "{#" "#}";	
